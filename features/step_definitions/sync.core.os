#Использовать tempfiles
#Использовать 1commands
#Использовать fs
// Реализация шагов BDD-фич/сценариев c помощью фреймворка https://github.com/artbear/1bdd

Перем БДД; //контекст фреймворка 1bdd
Перем ЛокальныеВременныеФайлы;
// Метод выдает список шагов, реализованных в данном файле-шагов
Функция ПолучитьСписокШагов(КонтекстФреймворкаBDD) Экспорт
	БДД = КонтекстФреймворкаBDD;

	ВсеШаги = Новый Массив;

	ВсеШаги.Добавить("ЯСоздаюВременныйКаталогИСохраняюЕгоВПеременной");
	ВсеШаги.Добавить("ЯСобираюТестовыйПлагинВКаталогеИзПеременной");
	ВсеШаги.Добавить("ЯСоздаюНовыйУправлениеплагинами");
	ВсеШаги.Добавить("ЯЗагружаюПлагиныИзКаталогаВПеременной");
	ВсеШаги.Добавить("ЯПодключаюПлагиныВМенеджерсинхронизации");

	Возврат ВсеШаги;
КонецФункции

// Реализация шагов

// Процедура выполняется перед запуском каждого сценария
Процедура ПередЗапускомСценария(Знач Узел) Экспорт
	
	ЛокальныеВременныеФайлы = Новый МенеджерВременныхФайлов;

КонецПроцедуры

// Процедура выполняется после завершения каждого сценария
Процедура ПослеЗапускаСценария(Знач Узел) Экспорт
	
КонецПроцедуры

//Я создаю временный каталог и сохраняю его в переменной "КаталогПлагинов"
Процедура ЯСоздаюВременныйКаталогИСохраняюЕгоВПеременной(Знач ИмяПеременной) Экспорт

	ВременныйКаталог = ЛокальныеВременныеФайлы.СоздатьКаталог();
	
	БДД.СохранитьВКонтекст(ИмяПеременной, ВременныйКаталог);
	
КонецПроцедуры

//Я собираю тестовый плагин в каталоге из переменной "КаталогПлагинов"
Процедура ЯСобираюТестовыйПлагинВКаталогеИзПеременной(Знач ИмяПеременнойКаталога) Экспорт
	
	КаталогПлагинов = БДД.ПолучитьИзКонтекста(ИмяПеременнойКаталога);

	ВременныйКаталог = ЛокальныеВременныеФайлы.СоздатьКаталог();
	
	ФС.КопироватьСодержимоеКаталога(КаталогТестовогоПлагина(), ВременныйКаталог);

	Команда = Новый Команда;
	Команда.УстановитьКоманду("opm");
	Команда.ДобавитьПараметр("build");
	Команда.ДобавитьПараметр(ВременныйКаталог);
	Команда.ДобавитьПараметр("-out");
	Команда.ДобавитьПараметр(ВременныйКаталог);
	КодВозврата = Команда.Исполнить();

	Если НЕ КодВозврата = 0 Тогда
		ВызватьИсключение Новый ИнформацияОбОшибке("Ошибка создания тестового плагина", Команда.ПолучитьВывод());
	КонецЕсли;

	Сообщить(Команда.ПолучитьВывод());

	МассивФайлов = НайтиФайлы(ВременныйКаталог, "*.ospx");

	Если МассивФайлов.Количество() = 0 Тогда
		ВызватьИсключение Новый ИнформацияОбОшибке("Ошибка создания тестового плагина", "Не найден собранный файл плагина");
	КонецЕсли;

	ФайлПлагина = МассивФайлов[0].ПолноеИмя;

	УправлениеПлагинами = БДД.ПолучитьИзКонтекста("УправлениеПлагинами");
	УправлениеПлагинами.УстановитьКаталогПлагинов(КаталогПлагинов);
	УправлениеПлагинами.УстановитьФайлПлагин(ФайлПлагина);

КонецПроцедуры

//Я создаю новый УправлениеПлагинами
Процедура ЯСоздаюНовыйУправлениеплагинами() Экспорт
	УправлениеПлагинами = Новый УправлениеПлагинами;
	
	БДД.СохранитьВКонтекст("УправлениеПлагинами", УправлениеПлагинами);
КонецПроцедуры

//Я загружаю плагины из каталога в переменной "КаталогПлагинов"
Процедура ЯЗагружаюПлагиныИзКаталогаВПеременной(Знач ИмяПеременнойКаталога) Экспорт

	УправлениеПлагинами = БДД.ПолучитьИзКонтекста("УправлениеПлагинами");
	КаталогПлагинов = БДД.ПолучитьИзКонтекста(ИмяПеременнойКаталога);

	УправлениеПлагинами.УстановитьКаталогПлагинов(КаталогПлагинов);
	УправлениеПлагинами.ЗагрузитьПлагины();

	УправлениеПлагинами.ВключитьПлагин("test_plugin");

КонецПроцедуры

//Я подключаю плагины в МенеджерСинхронизации
Процедура ЯПодключаюПлагиныВМенеджерсинхронизации() Экспорт

	УправлениеПлагинами = БДД.ПолучитьИзКонтекста("УправлениеПлагинами");
	
	ОбработчикПодписок = УправлениеПлагинами.НовыйМенеджерПодписок();

	МенеджерСинхронизации = БДД.ПолучитьИзКонтекста("МенеджерСинхронизации");
	МенеджерСинхронизации.ПодпискиНаСобытия(ОбработчикПодписок);

КонецПроцедуры

Функция КаталогТестовогоПлагина()
	Возврат ОбъединитьПути(ТекущийСценарий().Каталог, "testsata", "test_plugin");
КонецФункции

