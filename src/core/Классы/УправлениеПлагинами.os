#Использовать fs
#Использовать json
#Использовать logos
#Использовать "./internal"

Перем ИндексПлагинов;
Перем КаталогПлагинов;
Перем ПутьКФайлуВключенныхПлагинов;
Перем КаталогЗависимостей;

#Область Экспортные_методы

// Возвращает индекс плагинов
//
//  Возвращаемое значение:
//   Соответствие - набор плагинов 
//                   * ключ - Строка - имя плагина
//                   * значение - Объект - ссылка на произвольный объект 
//
Функция ПолучитьИндексПлагинов() Экспорт
	Возврат ИндексПлагинов;
КонецФункции

// Выполняет загрузку плагинов из каталога
//
Процедура ЗагрузитьПлагины() Экспорт

	ТекущийЗагрузчикПлагинов = Новый ЗагрузчикПлагинов(КаталогПлагинов);
	ТекущийЗагрузчикПлагинов.ЗагрузитьПлагины();
	ИндексПлагинов = ТекущийЗагрузчикПлагинов.ИндексПлагинов();

	ВключенныеПлагины = ПрочитатьВключенныеПлагины();
	ВключитьПлагины(ВключенныеПлагины);

КонецПроцедуры

// Создает и возвращает новый МенеджерПодписок для текущего индекса плагинов
//
//  Возвращаемое значение:
//   Объект.МенеджерПодписок - ссылка на новый объект класса <МенеджерПодписок>
//
Функция НовыйМенеджерПодписок() Экспорт
	
	Возврат Новый МенеджерПодписок(ИндексПлагинов);

КонецФункции

// Производит отключение массива плагинов
//
// Параметры:
//   МассивПлагинов - Массив - Элементы типа Строка, имена плагинов
//
Процедура ОтключитьПлагины(МассивПлагинов) Экспорт
	
	Для каждого ОтключаемыйПлагин Из МассивПлагинов Цикл
		
		Плагин = ИндексПлагинов[ОтключаемыйПлагин];

		Если Плагин = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Плагин.Отключить();

	КонецЦикла;

КонецПроцедуры

// Включает набор плагинов
//
// Параметры:
//   МассивПлагинов - Массив, Соответствие - набор наименований плагинов
//
Процедура ВключитьПлагины(МассивПлагинов) Экспорт
	
	Для каждого ВключаемыеПлагин Из МассивПлагинов Цикл
		
		Если ТипЗнч(ВключаемыеПлагин) = Тип("Строка") Тогда
			ВключитьПлагин(ВключаемыеПлагин);
		Иначе
			ВключитьПлагин(ВключаемыеПлагин.Ключ);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//   ИмяПлагина - <Тип.Вид> - <описание параметра>
//
Процедура ВключитьПлагин(Знач ИмяПлагина) Экспорт

	Плагин = ИндексПлагинов[ИмяПлагина];

	Если Плагин = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Плагин.Включить();

КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//   ПутьККаталогу - <Тип.Вид> - <описание параметра>
//
Процедура УстановитьКаталогПлагинов(Знач ПутьККаталогу) Экспорт
	КаталогПлагинов = ПутьККаталогу;
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//   НовыйКаталогЗависимостей - <Тип.Вид> - <описание параметра>
//
Процедура УстановитьКаталогЗависимостей(Знач НовыйКаталогЗависимостей) Экспорт
	КаталогЗависимостей = НовыйКаталогЗависимостей;
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//   ПутьКФайлуПакета - <Тип.Вид> - <описание параметра>
//
Процедура УстановитьФайлПлагин(Знач ПутьКФайлуПакета) Экспорт
	
	Установщик = Новый УстановщикПлагинов();
	Установщик.УстановитьКаталогПлагинов(КаталогПлагинов);
	Установщик.УстановитьКаталогЗависимостей(КаталогЗависимостей);

	Установщик.УстановитьФайлПлагина(ПутьКФайлуПакета);

КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//   ИмяПлагина - <Тип.Вид> - <описание параметра>
//
Процедура УстановитьПлагинПоИмени(Знач ИмяПлагина) Экспорт
	
	Установщик = Новый УстановщикПлагинов();
	Установщик.УстановитьКаталогПлагинов(КаталогПлагинов);
	Установщик.УстановитьКаталогЗависимостей(КаталогЗависимостей);

	Установщик.УстановитьПлагинПоИмени(ИмяПлагина);

КонецПроцедуры

// <Описание процедуры>
//
Процедура УстановитьРежимОтладки() Экспорт
	
	Для каждого КлючЗначение Из ИндексПлагинов Цикл
		
		Плагин = КлючЗначение.Значение;
		Плагин.ВключитьОтладку();

	КонецЦикла;

КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//   ПутьКФайлу - <Тип.Вид> - <описание параметра>
//
Процедура УстановитьФайлВключенныхПлагинов(Знач ПутьКФайлу) Экспорт
	ПутьКФайлуВключенныхПлагинов = ПутьКФайлу;
КонецПроцедуры

// <Описание функции>
//
//  Возвращаемое значение:
//   <Тип.Вид> - <описание возвращаемого значения>
//
Функция ПрочитатьВключенныеПлагины() Экспорт

	ВключенныеПлагины = Новый Соответствие;

	ПутьКФайлу = ПолучитьПутьКФайлуВключенныхПлагинов();
	ФайлАктивныхПлагинов= Новый Файл(ПутьКФайлу);

	Если Не ФайлАктивныхПлагинов.Существует() Тогда
		Возврат ВключенныеПлагины;
	КонецЕсли;

	JsonСтрока = ПрочитатьФайл(ПутьКФайлу);

	Если ПустаяСтрока(JsonСтрока) Тогда
		Возврат ВключенныеПлагины;
	КонецЕсли;

	ПарсерJSON  = Новый ПарсерJSON();
	ДанныеФайла = ПарсерJSON.ПрочитатьJSON(JsonСтрока);

	Для каждого ДанныеПлагинаИзФайла Из ДанныеФайла Цикл
		
		Если Булево(ДанныеПлагинаИзФайла.Значение) Тогда
			ВключенныеПлагины.Вставить(ДанныеПлагинаИзФайла.Ключ, ДанныеПлагинаИзФайла.Значение);
		КонецЕсли;

	КонецЦикла;

	Возврат ВключенныеПлагины;

КонецФункции

// <Описание функции>
//
//  Возвращаемое значение:
//   <Тип.Вид> - <описание возвращаемого значения>
//
Функция ИмяФайлаВключенныхПлагинов() Экспорт
	
	Возврат "gitsync3-plugins.json";

КонецФункции

// <Описание процедуры>
//
Процедура ЗаписатьВключенныеПлагины() Экспорт

	ИмяФайла = ПолучитьПутьКФайлуВключенныхПлагинов();

	КаталогФайла = Новый Файл(ИмяФайла).Путь;
	ФС.ОбеспечитьКаталог(КаталогФайла);

	ПарсерJSON  = Новый ПарсерJSON();
	ДанныеДляЗаписи = Новый Соответствие;

	Для каждого КлючЗначение Из ИндексПлагинов Цикл
		
		Плагин = КлючЗначение.Значение;
		ДанныеДляЗаписи.Вставить(Плагин.Имя(), Плагин.Включен());
	
	КонецЦикла;
	
	ТекстФайла = ПарсерJSON.ЗаписатьJson(ДанныеДляЗаписи);

	ЗаписатьФайл(ИмяФайла, ТекстФайла);

КонецПроцедуры

#КонецОбласти

#Область Вспомогательные_процедуры_и_функции

Функция ПолучитьПутьКФайлуВключенныхПлагинов()
	
	ФайлАктивныхПлагинов= Новый Файл(ПутьКФайлуВключенныхПлагинов);

	Если Не ФайлАктивныхПлагинов.Существует() Тогда
		Возврат ОбъединитьПути(КаталогПлагинов, ИмяФайлаВключенныхПлагинов());
	КонецЕсли;

	Возврат ФайлАктивныхПлагинов.ПолноеИмя;

КонецФункции

Функция ПрочитатьФайл(Знач ИмяФайла)
	
	Чтение = Новый ЧтениеТекста(ИмяФайла, КодировкаТекста.UTF8);
	Рез  = Чтение.Прочитать();
	Чтение.Закрыть();
	
	Возврат Рез;

КонецФункции

Процедура ЗаписатьФайл(Знач ИмяФайла, ТекстФайла)

	Запись = Новый ЗаписьТекста(ИмяФайла);
	Запись.ЗаписатьСтроку(ТекстФайла);
	Запись.Закрыть();

КонецПроцедуры

Процедура ПриСозданииОбъекта(Знач ЗначениеКаталогПлагинов = Неопределено)
	
	ИндексПлагинов = Новый Соответствие;

	Если ЗначениеЗаполнено(ЗначениеКаталогПлагинов) Тогда
		КаталогПлагинов = ЗначениеКаталогПлагинов;
	КонецЕсли;
	
	ДополнительныйКаталогБиблиотек = ПолучитьЗначениеСистемнойНастройки("lib.additional");

	Если ЗначениеЗаполнено(ДополнительныйКаталогБиблиотек) Тогда
		КаталогЗависимостей = ДополнительныйКаталогБиблиотек;
	Иначе
		КаталогЗависимостей = ПолучитьЗначениеСистемнойНастройки("lib.system");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
