#Использовать logos

Перем ИндексУстановленныхПлагинов; // Соответствие 
Перем Лог; // Объект - Логирование
Перем ДоступныйИнтерфейсПлагинов; // Объект.ИнтерфейсОбъекта - интерфейс плагинов
Перем КаталогПлагинов; // Строка - каталог пакетов плагинов

#Область Экспортные_методы

// Устанавливает каталог плагинов
//
// Параметры:
//   ПутьККаталогу - Строка - полный путь к каталогу плагинов
//
Процедура УстановитьКаталогПлагинов(Знач ПутьККаталогу) Экспорт
	КаталогПлагинов = ПутьККаталогу;
КонецПроцедуры

// Выполняет загрузки плагинов в индекс
//
Процедура ЗагрузитьПлагины() Экспорт
	
	ОбновитьИндексПлагинов();

КонецПроцедуры

// Возвращает загруженный индекс плагинов
//
//  Возвращаемое значение:
//   Соответствие - набор плагинов 
//                   * ключ - Строка - имя плагина
//                   * значение - Объект - ссылка на произвольный объект 
//
Функция ИндексПлагинов() Экспорт
	Возврат ИндексУстановленныхПлагинов;
КонецФункции

#КонецОбласти

#Область Вспомогательные_процедуры_и_функции

Процедура ОбновитьИндексПлагинов() 

	Лог.Отладка("Обновление индекса плагина из каталога <%1>", КаталогПлагинов);
	ИндексУстановленныхПлагинов = Новый Соответствие;

	ИндексПлагиновEXE = Новый ИндексПлагиновEXE;
	ИндексПлагинов = ИндексПлагиновEXE.ПолучитьИндексПлагиновEXE();

	Для каждого Плагин Из ИндексПлагинов Цикл

		ДобавитьПлагинВИндекс(Плагин);

	КонецЦикла;

	Лог.Отладка("В индекс плагинов добавлено <%1> плагинов", ИндексУстановленныхПлагинов.Количество());

КонецПроцедуры

Процедура ДобавитьПлагинВИндекс(Знач ИмяКлассаПлагина)
	
	Лог.Отладка("Добавляю плагин <%1> в индекс плагинов", ИмяКлассаПлагина);

	Попытка
		НовыйПлагин = Новый ПлагинСинхронизации(Неопределено, ИмяКлассаПлагина, ДоступныйИнтерфейсПлагинов);
		ИндексУстановленныхПлагинов.Вставить(НовыйПлагин.Имя(), НовыйПлагин);

	Исключение
		Лог.Отладка("Ошибка добавления плагина в индекс плагинов. По причине: <%1>", ОписаниеОшибки());
		
	КонецПопытки;


КонецПроцедуры

Процедура ПриСозданииОбъекта(Знач ВходящийКаталогПлагинов = Неопределено)
	
	КаталогПлагинов = ВходящийКаталогПлагинов;
	ДоступныйИнтерфейсПлагинов = Новый ИнтерфейсПлагинов().Интерфейс();
	Лог = Логирование.ПолучитьЛог("oscript.lib.gitsync.plugins.loader");

КонецПроцедуры

#КонецОбласти
