#Использовать "../../core"

Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("a all", Ложь, "Отключить все включенные плагины")
					.ВОкружении("GITSYNC_DISABLE_ALL_PLUGINS");
	Команда.Аргумент("PLUGIN", "", "Имя плагина")
					.ТМассивСтрок()
					.ВОкружении("GITSYNC_PLUGINS");
	
	Команда.Спек = "(-a | --all) | PLUGIN...";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ИменаПлагинов = Команда.ЗначениеАргумента("PLUGIN");
	ВсеВключенные = Команда.ЗначениеОпции("all");

	ОбщиеПараметры = ПараметрыПриложения.Параметры();
	МенеджерПлагинов = ОбщиеПараметры.УправлениеПлагинами;
	
	ВсеПлагины = МенеджерПлагинов.ПолучитьИндексПлагинов();
	
	Лог = ПараметрыПриложения.Лог();
	Лог.Отладка("Отключение всех плагинов <%1>", ВсеВключенные);

	ПлагиныДляОтключения = Новый Массив;

	Если ВсеВключенные Тогда
		Для каждого Плагин Из ВсеПлагины Цикл
			Лог.Отладка(" --> отключаю плагин <%1>", Плагин.Ключ);
			ПлагиныДляОтключения.Добавить(Плагин.Ключ);
		КонецЦикла;
	Иначе
		ПлагиныДляОтключения = ИменаПлагинов;
	КонецЕсли;
	
	МенеджерПлагинов.ОтключитьПлагины(ПлагиныДляОтключения);

	МенеджерПлагинов.ЗаписатьВключенныеПлагины();

КонецПроцедуры
