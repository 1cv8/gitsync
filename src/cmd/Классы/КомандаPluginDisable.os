#Использовать "../../core"

Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("a all", Ложь, "Отключить все включенные плагины")
					.ВОкружении("GITSYNC_DISABLE_ALL_PLUGINS");
	
	Команда.Опция("i interactive", Ложь, "интерактивный выбор плагинов для отключения")
				.Флаг();

	Команда.Аргумент("PLUGIN", "", "Имя плагина")
					.ТМассивСтрок()
					.ВОкружении("GITSYNC_PLUGINS");
	
	Команда.Спек = "(-a | --all) | (-i | --interactive) | PLUGIN...";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ИменаПлагинов = Команда.ЗначениеАргумента("PLUGIN");
	ВсеВключенные = Команда.ЗначениеОпции("all");
	ВыбратьПлагиныИнтерактивно = Команда.ЗначениеОпции("interactive");

	МенеджерПлагинов = ПараметрыПриложения.МенеджерПлагинов();
	
	Лог = ПараметрыПриложения.Лог();
	Лог.Отладка("Отключение всех плагинов <%1>", ВсеВключенные);

	Если ВсеВключенные Тогда

		МенеджерПлагинов.ОтключитьВсеПлагины();

	Иначе
	
		Если ВыбратьПлагиныИнтерактивно Тогда
			ВсеПлагины = МенеджерПлагинов.ПолучитьИндексПлагинов();

			ВыборВКонсоли = Новый ВыборВКонсоли("Выберите плагины к отключению:");
			Для каждого Плагин Из ВсеПлагины Цикл
				Если Плагин.Значение.Включен() Тогда
					ИмяПлагина = Плагин.Ключ;
				Иначе
					ИмяПлагина = Лев(Плагин.Ключ + "              ", 15) + " - отключен";
				КонецЕсли;
				ВыборВКонсоли.ДобавитьЗначениеВыбора(ИмяПлагина, , Плагин.Значение.Включен());
			КонецЦикла;
			ИменаПлагинов = ВыборВКонсоли.Выбрать();
		КонецЕсли;

		МенеджерПлагинов.ОтключитьПлагины(ИменаПлагинов);

		Если ЗначениеЗаполнено(ИменаПлагинов) Тогда
			Сообщить("Выключены плагины: " + СтрСоединить(ИменаПлагинов, ", "));
		КонецЕсли;
		
	КонецЕсли;

	ПараметрыПриложения.ЗаписатьВключенныеПлагины();

КонецПроцедуры
