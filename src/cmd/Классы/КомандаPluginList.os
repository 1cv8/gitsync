#Использовать "../../core"

Перем Лог;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("q quiet", Ложь, "вывод только названий плагинов");
	Команда.Опция("a all", Ложь, "выводит все установленные плагины");
	Команда.Аргумент("NAME", "", "Строка поиска установленного плагина");

	Команда.Спек = "[-q | --quiet] ((-a | --all) | [NAME])";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ИменаПлагинов		= Команда.ЗначениеАргумента("NAME");
	ВывестиВсеПлагины	= Команда.ЗначениеОпции("-a");
	ВыводТолькоНазваний	= Команда.ЗначениеОпции("-q");

	ОбщиеПараметры = ПараметрыПриложения.Параметры();
	МенеджерПлагинов = ОбщиеПараметры.УправлениеПлагинами;
	
	ВсеПлагины = МенеджерПлагинов.ПолучитьИндексПлагинов();
	ВключенныеПлагины = МенеджерПлагинов.ПрочитатьВключенныеПлагины();

	Если НЕ ВыводТолькоНазваний Тогда
		Сообщить("Список плагинов");
	КонецЕсли;

	Для каждого Плагин Из ВсеПлагины Цикл
		
		ВключенныйПлагин = ВключенныеПлагины[Плагин.Ключ];

		Если ВключенныйПлагин = Неопределено
			И НЕ ВывестиВсеПлагины Тогда
			Продолжить;
		КонецЕсли;

		ИнформацияОПлагине = Плагин.Значение;

		Если ВыводТолькоНазваний Тогда
	
			ВывестиИмяПлагине(ИнформацияОПлагине);
		
		Иначе
		
			ВывестиИнформациюОПлагине(ИнформацияОПлагине);
		
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

Функция ВывестиИмяПлагине(Знач ОписаниеПлагина) Экспорт
	
	Сообщить(ОписаниеПлагина.ИмяПлагина);
	
КонецФункции

Функция ВывестиИнформациюОПлагине(Знач ОписаниеПлагина) Экспорт
	
	Сообщить(СтрШаблон("%1 - v %2", ОписаниеПлагина.ИмяПлагина, ОписаниеПлагина.Описание.Версия));
	
КонецФункции

Лог = ПараметрыПриложения.Лог();