#Использовать "../../core"

Перем Лог;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("q quiet", Ложь, "вывод только названий плагинов");
	Команда.Опция("a all", Ложь, "выводит все установленные плагины");

	// Команда.Спек = "[-q | --quiet] ((-a | --all) | [NAME])";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ВывестиВсеПлагины	= Команда.ЗначениеОпции("all");
	ВыводТолькоНазваний	= Команда.ЗначениеОпции("quiet");

	ОбщиеПараметры = ПараметрыПриложения.Параметры();
	МенеджерПлагинов = ОбщиеПараметры.УправлениеПлагинами;
	
	ВсеПлагины = МенеджерПлагинов.ПолучитьИндексПлагинов();
	ВключенныеПлагины = МенеджерПлагинов.ПрочитатьВключенныеПлагины();

	Если НЕ ВыводТолькоНазваний Тогда
		Сообщить(СтрШаблон("Каталог плагинов: <%1>", ПараметрыПриложения.КаталогПлагинов()));
		Сообщить("Список плагинов: ");
	КонецЕсли;

	Для каждого Плагин Из ВсеПлагины Цикл
		
		ВключенныйПлагин = ВключенныеПлагины[Плагин.Ключ];

		Если ВключенныйПлагин = Неопределено
			И НЕ ВывестиВсеПлагины Тогда
			Продолжить;
		КонецЕсли;

		КлассПлагина = Плагин.Значение;

		Если ВыводТолькоНазваний Тогда
	
			Сообщить(КлассПлагина.Имя());
		
		Иначе
		
			ВывестиОписаниеПлагина(КлассПлагина);

		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

Процедура ВывестиОписаниеПлагина(КлассПлагина);

	ШаблонНаименования =  " %4 %1 [%3] " + Символы.Таб + "%2";
	ТекстВключен = ?(КлассПлагина.Включен(), "[on] ", "[off]");

	Сообщить(СтрШаблон(ШаблонНаименования, КлассПлагина.Имя(), КлассПлагина.Описание(), КлассПлагина.Версия(), ТекстВключен));

КонецПроцедуры

Лог = ПараметрыПриложения.Лог();