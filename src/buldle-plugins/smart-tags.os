#Использовать logos

Перем ВерсияПлагина;
Перем Лог;
Перем Обработчик;
Перем ПоследнняяВерсияКонфигурации;
Перем НоваяМетка;

Функция Информация() Экспорт
	
	Возврат Новый Структура("Версия, Лог", ВерсияПлагина, Лог)
	
КонецФункции // Информация() Экспорт

Процедура ПриАктивизацииПлагина(СтандартныйОбработчик) Экспорт
	
	Обработчик = СтандартныйОбработчик;
	ПоследнняяВерсияКонфигурации = 0;

КонецПроцедуры

Процедура ПередНачаломЦиклаОбработкиВерсий(ТаблицаИсторииХранилища, ТекущаяВерсия, СледующаяВерсия, МаксимальнаяВерсияДляРазбора) Экспорт
	
	СтрокаТекущейВерсии = ТаблицаИсторииХранилища.Найти(СледующаяВерсия, "НомерВерсии");
	ОтправитьНовыеМетки = Ложь;
	Если СтрокаТекущейВерсии <> Неопределено Тогда
		ПоследнняяВерсияКонфигурации = СтрокаТекущейВерсии.Тэг;
	КонецЕсли;

КонецПроцедуры

Процедура ПередОбработкойВерсииХранилища(СтрокаВерсии, СледующаяВерсия) Экспорт	

	НоваяМетка = "";
	Если ПоследнняяВерсияКонфигурации <> СтрокаВерсии.Тэг Тогда
		ОтправитьНовыеМетки = Истина;
		Лог.Информация("Определена новая версия конфигурации: %1 будет установлен новый тэг", СтрокаВерсии.Тэг);
		НоваяМетка = СтрокаВерсии.Тэг;
		ПоследнняяВерсияКонфигурации = НоваяМетка;
	КонецЕсли;

КонецПроцедуры

Процедура ПослеКоммита(ГитРепозиторий, КаталогРабочейКопии) Экспорт
	
	Если ЗначениеЗаполнено(НоваяМетка) Тогда
		ПараметрыКоманды = Новый Массив;
		ПараметрыКоманды.Добавить("tag");
		ПараметрыКоманды.Добавить(Строка(НоваяМетка));
	
		ГитРепозиторий.ВыполнитьКоманду(ПараметрыКоманды);
		
	КонецЕсли;

КонецПроцедуры

Функция Форматировать(Знач Уровень, Знач Сообщение) Экспорт
	
	Возврат СтрШаблон("[PLUGIN] %1: %2 - %3", ИмяПлагина(), УровниЛога.НаименованиеУровня(Уровень), Сообщение);
	
КонецФункции

Функция ИмяПлагина()
	возврат "smart-tags";
КонецФункции // ИмяПлагина()

Процедура Инициализация()

	ВерсияПлагина = "1.0.0";
	Лог = Логирование.ПолучитьЛог("oscript.app.gitsync.plugins."+ ИмяПлагина());
	КомандыПлагина = Новый Массив;
	КомандыПлагина.Добавить("sync");
	КомандыПлагина.Добавить("export");
	ПоследнняяВерсияКонфигурации = Неопределено;
	Лог.УстановитьРаскладку(ЭтотОбъект);

КонецПроцедуры

Инициализация();
