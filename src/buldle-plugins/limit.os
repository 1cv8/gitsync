
#Использовать logos

Перем ВерсияПлагина;
Перем Лог;
Перем КомандыПлагина;
Перем МассивНомеровВерсий;
Перем Лимит;
Перем Обработчик;

Функция Информация() Экспорт
	
	Возврат Новый Структура("Версия, Лог", ВерсияПлагина, Лог)
	
КонецФункции // Информация() Экспорт

Процедура ПриАктивизацииПлагина(СтандартныйОбработчик, КонтекстПлагина) Экспорт
	
	Обработчик = СтандартныйОбработчик;

КонецПроцедуры


Процедура ПриРегистрацииКомандыПриложения(ИмяКоманды, КлассРеализации, Парсер, КонтекстПлагина) Экспорт

	Лог.Отладка("Ищю команду <%1> в списке поддерживаемых", ИмяКоманды);
	Если КомандыПлагина.Найти(ИмяКоманды) = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Лог.Отладка("Устанавливаю дополнительные параметры для команды %1", ИмяКоманды);
	
	ОписаниеКоманды = Парсер.ПолучитьКоманду(ИмяКоманды);
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "-limit", "[PLUGIN] [limit] <выгрузить не более limit версий от текущей выгруженной>");
	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры

Процедура ПриВыполненииКоманды(ПараметрыКоманды, ДополнительныеПараметры, КонтекстПлагина) Экспорт
	
	Лимит = ПараметрыКоманды["-limit"];
	
	Если Лимит = Неопределено Тогда
		Лимит = 0;
	КонецЕсли;
	
	Лимит = Число(Лимит);

	Лог.Отладка("[plugin-limit] Установлен лимит <%1> выгрузки версий", Лимит);

КонецПроцедуры

Процедура ПередНачаломЦиклаОбработкиВерсий(ТаблицаИсторииХранилища, ТекущаяВерсия, СледующаяВерсия, МаксимальнаяВерсияДляРазбора, КонтекстПлагина) Экспорт 

	КонечнаяВерсия = 0;
	
	Если Лимит > 0 Тогда
		
		СтрокаТекущейВерсии = ТаблицаИсторииХранилища.Найти(ТекущаяВерсия, "НомерВерсии");
		ИндексСтрокиТекущейВерсии = ТаблицаИсторииХранилища.Индекс(СтрокаТекущейВерсии);
		ИндексСтрокиСОграничением = Мин(ТаблицаИсторииХранилища.Количество() - 1, ИндексСтрокиТекущейВерсии + Лимит);
		НомерВерсииСогласноЛимита = ТаблицаИсторииХранилища[ИндексСтрокиСОграничением].НомерВерсии;
		
		Если КонечнаяВерсия = 0 Тогда
			КонечнаяВерсия = НомерВерсииСогласноЛимита;
		Иначе
			КонечнаяВерсия = ?(КонечнаяВерсия >= НомерВерсииСогласноЛимита, КонечнаяВерсия, НомерВерсииСогласноЛимита);
		КонецЕсли;
		
	КонецЕсли;

	МаксимальнаяВерсияДляРазбора = КонечнаяВерсия;

КонецПроцедуры

Процедура Инициализация()

	ВерсияПлагина = "1.0.0";
	Лог = Логирование.ПолучитьЛог("oscript.app.gitsync.plugins.limit");
	КомандыПлагина = Новый Массив;
	КомандыПлагина.Добавить("sync");
	КомандыПлагина.Добавить("export");
	Лимит = 0;

КонецПроцедуры

Инициализация();


